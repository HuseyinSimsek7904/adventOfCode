macro malloc:ARRAY-SIZE 0x100000 endmacro
macro malloc:AVAIL-STACK-SIZE 0x100 endmacro

include "dynamic/malloc.corth"
include "dynamic/utils.corth"
include "linux_x86/stdio.corth"
include "collections/list64.corth"


// ptr: list int: remove -> int: length
macro length-after-remove isn-neg if list64:length dec else list64:length end endmacro

// int: index ptr: list int: remove -> int: index
macro get-after-remove let _index_ _list_ _remove_ in _index_ _remove_ >= if _index_ inc else _index_ end _list_ list64:get end endmacro


proc check-if-safe
  ptr int -> bool
in let list remove in
  list remove length-after-remove 1 <= if true return end

  0 list remove get-after-remove 1 list remove get-after-remove let first second in
    second first - abs 3 >
    second first = | if false return end

    second first >
  end

  memory ascending sizeof(bool) in
    ascending !bool

    1 while dup list remove length-after-remove < do let i in
      i dec list remove get-after-remove i list remove get-after-remove let left right in
        right left > let cmp in
          right left =
          right left - abs 3 > |
          cmp ascending @bool ^ | if false return end
        end
      end
    i end inc end drop
  end
end true end


proc main
  int int -> int
in let argc argv in
  malloc:init

  "data.txt" drop fopen-r let file in
    file isn-pos if
      "could not open file\n" eputs
      1 exit drop
    end

    memory safe      sizeof(int) and
           list      sizeof(ptr) and
           token     0x10        and
           length    sizeof(int) and
           buffer    1           in

      0 safe !64
      0 length !64

      list64:init list !64

      while true do
        buffer 1 file fgets is-zero if
          safe @64 putu putnl
          0 return
        end

        buffer @8 ' ' = if
          token length @64 length convd drop
          length @64 list list64:append
          0 length !64

        else buffer @8 '\n' = if
          token length @64 length convd drop
          length @64 list list64:append
          0 length !64

          -1 while dup list list64:length < do peek i in
            list i check-if-safe if safe @inc64 break end
          end inc end drop

          0 list list64:length-addr !64

        else
          buffer @8 length @64 token array8:set
          length @inc64

        end end
      end
    end

    file fclose drop
  end
end 0 end
